#!/bin/zsh

# options
local p=:server:nosql
local db=$p:default
local port=5050
local sf=znosql.zdb
while getopts d:p:s: name
do
    case $name in
        d) db=$p:$OPTARG ;;
        p) port=$OPTARG ;;
		s) sf=$OPTARG.zdb ;;
    esac
done

# init
zmodload zsh/net/tcp
zmodload zsh/zselect

ztcp -l $port
local pfd=$REPLY
trap "ztcp -c; exit" EXIT INT
local -A fds=()
local -A val=() # default values

handle() {
	local get=
	local line=()

	read -A line <& $1
	case $line[1] in
		""|exit|quit) # broken pipe / close connection
			ztcp -c $1
			unset "fds[$1]"
			unset "val[$1]"
			;;
		shutdown)
			ztcp -c
			exit 0
			;;
		ping) echo pong >& $1 ;;
		switch) fds[$1]=$line[2] ;;
		get)
			zstyle -s $2 $line[2] get || get=$val[$1]
			echo $get >& $1
			;;
		getd)
			zstyle -s $2 $line[2] get || get=$line[3]
			echo $get >& $1
			;;
		put)  zstyle $2 $line[2] "$line[3,-1]" ;;
		putd) val[$1]="$line[2,-1]" ;;
		dump) zstyle -L "$p:*" ;;
		save) zstyle -L "$p:*" > "$sf" ;;
		load)
			zstyle -d
			source "$sf" ;;
	esac
}

[ -r "$sf" ] && source "$sf"
while [ 1 ]
do
	local -A READY=()
	# wait for an available fd
	zselect -rA READY ${(@k)fds} $pfd
	for fd in "${(@k)READY}"; do
		if [[ $fd = $pfd ]]; then
			ztcp -a $pfd
			fds[$REPLY]=$db
			continue
		fi
		handle $fd "$fds[$fd]"
	done
done
